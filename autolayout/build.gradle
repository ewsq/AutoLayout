apply plugin: 'com.android.library'

android {
    compileSdkVersion COMPILE_SDK_VERSION
    buildToolsVersion BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion MIN_SDK_VERSION
        targetSdkVersion TARGET_SDK_VERSION
        versionCode 1
        versionName "1.4.5"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}


dependencies {
    compile libSupportDesign
    compile libSupportCardView
    compile libSupportGridlayout
    compile libSupportAppcompat_v4
    compile libSupportAppcompat_v7
}


/***************发布aar包的配置**********************/
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

//项目引用的版本号;
version = "1.1.5"

//定义两个链接，下面会用到。
def siteUrl = 'https://github.com/nansir/AutoLayout'    // 项目主页
def gitUrl = 'git@github.com:nansir/AutoLayout.git' // Git仓库的url

//唯一包名
group = "com.sir.library"

install {
    repositories.mavenInstaller {
        // 生成pom.xml和参数
        pom {
            project {
                packaging 'aar'
                // 项目描述
                name 'AutoLayout'//项目名称
                description 'The Android project completes the screen adaptation plan.'//项目描述
                url siteUrl // 项目主页，这里是引用上面定义

                // 软件开源协议
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                //填写开发者基本信息
                developers {
                    developer {
                        id 'nansir' // 开发者的id
                        name 'nansir' // 开发者名字
                        email '445181052@qq.com' // 开发者邮箱
                    }
                }

                // SCM
                scm {
                    connection gitUrl // Git仓库地址
                    developerConnection gitUrl // Git仓库地址
                    url siteUrl // 项目主页。
                }
            }
        }
    }
}
// 生成jar包的task，不需要修改。
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
// 生成jarDoc的task，不需要修改。
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    // destinationDir = file("../javadoc/")
    failOnError false // 忽略注释语法错误，如果用jdk1.8你的注释写的不规范就编译不过。
}
// 生成javaDoc的jar，不需要修改。
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}
javadoc {
    options {
        encoding "UTF-8"
        charSet 'UTF-8'
        author true
        version true
        links "http://docs.oracle.com/javase/7/docs/api"
    }
}

// 这里是读取Bintray相关的信息，我们上传项目到github上的时候会把gradle文件传上去，所以不要把帐号密码的信息直接写在这里，写在local.properties中，这里动态读取。
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user") // Bintray的用户名。
    key = properties.getProperty("bintray.apikey") // Bintray刚才保存的ApiKey
    configurations = ['archives']
    pkg {
        repo = "Android"
        // 上传到maven库。（这里要特别注意，如果写了maven报404错误，请在bintray创建一个仓库，这里填改成你创建的仓库的名字。）
        name = "AutoLayout"  // 发布到Bintray上的项目名字
        userOrg = 'nansir' // Bintray的用户名
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        labels = ['android']
        publicDownloadNumbers = true
        publish = true // 是否是公开项目
    }
}

// Terminal 窗口输入gradlew install 开始编译
// Terminal 窗口输入gradlew bintrayUpload 开始上传